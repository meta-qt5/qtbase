#
# qmake configuration for linux-g++ with modifications for building with OpenEmbedded
#

MAKEFILE_GENERATOR = UNIX
CONFIG += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)

# QMAKE_<TOOL> (moc, uic, rcc) are gone, overwrite only ar and strip
QMAKE_AR              = $$(OE_QMAKE_AR) cqs
QMAKE_STRIP           = $$(OE_QMAKE_STRIP)

include(../common/gcc-base-unix.conf)

# *FLAGS from gcc-base.conf
QMAKE_CFLAGS                += $$(OE_QMAKE_CFLAGS)
QMAKE_CXXFLAGS              += $$(OE_QMAKE_CXXFLAGS)
QMAKE_LFLAGS                += $$(OE_QMAKE_LDFLAGS)

include(../common/clang.conf)

# tc settings from g++-base.conf
QMAKE_CC       = $$(OE_QMAKE_CC)
QMAKE_CXX      = $$(OE_QMAKE_CXX)

QMAKE_LINK         = $$(OE_QMAKE_LINK)
QMAKE_LINK_SHLIB   = $$(OE_QMAKE_LINK)
QMAKE_LINK_C       = $$(OE_QMAKE_LINK)
QMAKE_LINK_C_SHLIB = $$(OE_QMAKE_LINK)
QMAKE_CFLAGS_ISYSTEM =
# for the SDK
isEmpty(QMAKE_QT_CONFIG):QMAKE_QT_CONFIG = $$(OE_QMAKE_QT_CONFIG)

include(../oe-device-extra.pri)

load(device_config)
load(qt_config)
